#!/usr/local/bin/perl
use strict;
use warnings;
use File::Copy;
use File::Path 'make_path';
use feature 'say';

my ($change_git_config, $change_emerge_config, $build_type);

print "Enter the directory to act as Emerge root: [$ENV{'HOME'}/kderoot] ";
chomp (my $kderoot_path = <>);
$kderoot_path = "$ENV{'HOME'}/kderoot" if ! $kderoot_path;

chomp (my $out = `git config --global --get url.git://anongit.kde.org/.insteadOf`);
if (! $out) {
    ++$change_git_config
} elsif ($out ne 'kde:') {
    print "The insteadOf parameter of your ~/.gitconfig is \"$out\" but it should be \"kde:\"\nSet it to \"kde:\"? [yes] ";
    chomp (my $set = <>);
    ++$change_git_config if ! $set or $set =~ /^(?:yes|y)$/i
}

while (1) {
    print 'Select BuildType (Release/Debug/MinSizeRel/RelWithDebInfo): [RelWithDebInfo] ';
    chomp ($build_type = <>);
    last if ! $build_type or $build_type eq 'RelWithDebInfo'; 
    ++$change_emerge_config and last if $build_type =~ /^(?:Release|Debug|MinSizeRel)$/
}

make_path $kderoot_path;
chdir $kderoot_path;

say 'Something wrong with changing gitconfig' if $change_git_config and system 'git config --global url.git://anongit.kde.org/.insteadOf "kde:"'; 

die 'git clone error' if system 'git clone git://anongit.kde.org/emerge.git';
chdir 'emerge';
say `git checkout unix3`;
chdir '..';
mkdir 'etc' ;

if ($change_emerge_config) {
    open my $ini, 'emerge/kdesettings.fbsd';
    open my $finini, '>etc/kdesettings.ini';
    while (<$ini>) {
        s/(=\s)RelWithDebInfo/$1$build_type/;
        print $finini $_
    }
} else {
    copy 'emerge/kdesettings.fbsd', 'etc/kdesettings.ini' or die "copy:$!" 
}

say "Bootstrapping done. Now start bash and run \"source $kderoot_path/emerge/kdeenv.sh\"";
